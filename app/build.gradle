plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.huawei.agconnect'
}

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file("keystore/keystore.properties")))

android {
    namespace "com.thalesgroup.tshpaysample"

    defaultConfig {
        applicationId "com.thalesgroup.tshpaysample"
        minSdkVersion 24
        targetSdkVersion 34
        compileSdk 34
        versionCode 12
        versionName "1.2"
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file("${project.rootDir}/${keystoreProperties['debugStoreFile']}")
            storePassword keystoreProperties['debugStorePassword']
            keyAlias keystoreProperties['debugKeyAlias']
            keyPassword keystoreProperties['debugKeyPassword']
        }
        release {
            storeFile file("${project.rootDir}/${keystoreProperties['releaseStoreFile']}")
            storePassword keystoreProperties['releaseStorePassword']
            keyAlias keystoreProperties['releaseKeyAlias']
            keyPassword keystoreProperties['releaseKeyPassword']
        }
    }

    flavorDimensions "paymentExperience"
    productFlavors {
        // Payment experience
        oneTap {
            dimension "paymentExperience"
            buildConfigField "String", "PAYMENT_EXPERIENCE_VARIANT", "\"ONE_TAP\""
        }
        twoTap {
            dimension "paymentExperience"
            buildConfigField "String", "PAYMENT_EXPERIENCE_VARIANT", "\"TWO_TAP\""
        }
    }

    // Create link between environment and flavour
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {

    // Android support library
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.google.android.gms:play-services-base:18.5.0'
    implementation "androidx.biometric:biometric:1.1.0"

    // TSH Pay SDK
    debugImplementation (files("libs/TSHPaySDK-dev-6.10.0.rc01.aar"))
    releaseImplementation (files("libs/TSHPaySDK-release-6.10.0.rc01.aar"))

    // SDK Dependency
    implementation 'net.java.dev.jna:jna:5.6.0@aar'

    // FCM - Google push notifications
    implementation platform('com.google.firebase:firebase-bom:33.10.0')
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-analytics'

    // HMS - Huawei push notifications.
    implementation 'com.huawei.hms:push:6.3.0.302'

    // JSON Support
    implementation 'com.google.code.gson:gson:2.10.1'

    // Multi-dex application to prevent any size issue.
    implementation 'androidx.multidex:multidex:2.0.1'
}