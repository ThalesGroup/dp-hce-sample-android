plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.huawei.agconnect'
}

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file("keystore/keystore.properties")))

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.thalesgroup.tshpaysample"
        minSdkVersion 23
        targetSdkVersion 32
        versionCode 1
        versionName "1.1"

        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file("${project.rootDir}/${keystoreProperties['debugStoreFile']}")
            storePassword keystoreProperties['debugStorePassword']
            keyAlias keystoreProperties['debugKeyAlias']
            keyPassword keystoreProperties['debugKeyPassword']
        }
        release {
            storeFile file("${project.rootDir}/${keystoreProperties['releaseStoreFile']}")
            storePassword keystoreProperties['releaseStorePassword']
            keyAlias keystoreProperties['releaseKeyAlias']
            keyPassword keystoreProperties['releaseKeyPassword']
        }
    }

    flavorDimensions "paymentExperience"
    productFlavors {
        // Payment experience
        oneTapForeground {
            dimension "paymentExperience"
            versionNameSuffix "-oneTapForeground"
        }
        oneTap {
            dimension "paymentExperience"
            versionNameSuffix "-oneTap"
        }
        twoTap {
            dimension "paymentExperience"
            versionNameSuffix "-twoTap"
        }
    }

    // Create link between environment and flavour
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    // Android support library
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.google.android.gms:play-services-base:18.0.1'
    implementation "androidx.biometric:biometric:1.1.0"

    // TSH Pay SDK
    debugImplementation (files("libs/TSHPaySDK-dev-6.6.0.rc01.aar"))
    releaseImplementation (files("libs/TSHPaySDK-release-6.6.0.rc01.aar"))

    // SDK Dependency
    implementation 'net.java.dev.jna:jna:5.6.0@aar'

    // FCM - Google push notifications
    implementation platform('com.google.firebase:firebase-bom:28.0.1')
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-analytics'

    // HMS - Huawei push notifications.
    implementation 'com.huawei.hms:push:6.3.0.302'

    // JSON Support
    implementation 'com.google.code.gson:gson:2.8.7'

    // Multi-dex application to prevent any size issue.
    implementation 'androidx.multidex:multidex:2.0.1'
}